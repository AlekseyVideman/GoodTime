plugins {
    id 'java'
    id 'idea'
//    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "io.github.goooler.shadow" version "8.1.8"
}

sourceCompatibility = "21"
targetCompatibility = "21"

repositories {
    mavenCentral()

    maven {
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    implementation 'com.maxmind.geoip2:geoip2:2.15.0'

    implementation "com.google.inject:guice:7.0.0"
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

idea {
    module {
        downloadJavadoc(true)
    }
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveVersion.set(pluginVersion)
    archiveClassifier.set(null)

    minimize()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named("processResources") {
    filesMatching("plugin.yml") {
        expand([
                version: pluginVersion,
                plugin : rootProject.name
        ])
    }
}

tasks.build.dependsOn(shadowJar)
jar.enabled = false

tasks.register('movePluginToServer') {
    dependsOn(build)
    doLast {
        def pathToServer = "E:/DevCraft"
        def pluginFileName = project.name + "-" + pluginVersion + ".jar"
        ant.move(file: buildDir.getAbsolutePath() + "/libs/" + pluginFileName, tofile: pathToServer + "/plugins/" + pluginFileName)
    }
}